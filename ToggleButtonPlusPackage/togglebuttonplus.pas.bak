
unit ToggleButtonPlus;

{$mode objfpc}{$H+}

interface

uses
  Classes, SysUtils, Graphics, LCLType, LCLIntf, Controls, Types, LMessages,
  LResources, ExtCtrls, StdCtrls, Math;

type

  { TToggleButtonPlus }

  TToggleButtonPlus = class(TCustomControl)
  private
    FBorderColor: TColor;
    FBorderEnabled: Boolean;
    FBorderLineStyle: TPenStyle;
    FChecked: Boolean;
    FCaption: String;
    FCornerRadius: Integer;
    FOnChange: TNotifyEvent;
    procedure SetBorderEnabled(AValue: Boolean);
    procedure SetBorderLineStyle(AValue: TPenStyle);
    procedure SetChecked(AValue: Boolean);
    procedure SetCaption(AValue: String);
    procedure SetBorderRadius(AValue: Integer);
  protected
    procedure Paint; override;
    procedure Click; override;
  public
    constructor Create(AOwner: TComponent); override;
  published
    property Align;
    property Anchors;
    property Enabled;
    property Visible;
    property OnChange: TNotifyEvent read FOnChange write FOnChange;

    property Checked: Boolean read FChecked write SetChecked default False;
    property Caption: String read FCaption write SetCaption;
    property BorderStyle: TPenStyle read FBorderLineStyle write SetBorderLineStyle default psSolid;
    property BorderEnabled: Boolean read FBorderEnabled write SetBorderEnabled default True;
    property BorderColor: TColor read FBorderColor write FBorderColor default clGray;
    property BorderRadius: Integer read FCornerRadius write SetBorderRadius default 6;
  end;

procedure Register;

implementation

procedure Register;
begin
  {$I togglebuttonplus_icon.lrs}
  RegisterComponents('ModernUI', [TToggleButtonPlus]);
end;

constructor TToggleButtonPlus.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  Width := 70;
  Height := 25;
  FChecked := False;
  FCaption := '';
  ControlStyle := ControlStyle + [csOpaque];
end;

procedure TToggleButtonPlus.SetChecked(AValue: Boolean);
begin
  if FChecked = AValue then Exit;
  FChecked := AValue;
  Invalidate;
  if Assigned(FOnChange) then FOnChange(Self);
end;

procedure TToggleButtonPlus.SetBorderEnabled(AValue: Boolean);
begin
  if FBorderEnabled = AValue then Exit;
  FBorderEnabled := AValue;
  Invalidate;
end;

procedure TToggleButtonPlus.SetBorderLineStyle(AValue: TPenStyle);
begin
  if FBorderLineStyle = AValue then Exit;
  FBorderLineStyle := AValue;
  Invalidate;
end;

procedure TToggleButtonPlus.SetCaption(AValue: String);
begin
  if FCaption = AValue then Exit;
  FCaption := AValue;
  Invalidate;
end;

procedure TToggleButtonPlus.SetBorderRadius(AValue: Integer);
begin
  if FCornerRadius = AValue then Exit;
  FCornerRadius := AValue;
end;

procedure TToggleButtonPlus.Click;
begin
  inherited Click;
  Checked := not Checked;
end;

procedure TToggleButtonPlus.Paint;
var
  Radius, BallSize, BallPos: Integer;
  BGColor, BallColor: TColor;
  SwitchWidth: Integer;
  TextRect: TRect;
begin
  Canvas.Font := Font;
  Canvas.Brush.Style := bsSolid;
  Canvas.Pen.Style := psClear;

  Radius := Height div 2;
  BallSize := Height - 4;
  SwitchWidth := Height * 2;

  // Define cor de fundo ON/OFF
  if Checked then
    BGColor := $99FF99 // Verde claro
  else
    BGColor := clSilver;

  // Desenha fundo arredondado (switch)
  Canvas.Brush.Color := BGColor;
  Canvas.RoundRect(0, 0, SwitchWidth, Height, Radius, Radius);

  // Desenha círculo (bola)
  if Checked then
    BallPos := SwitchWidth - BallSize - 2
  else
    BallPos := 2;

  Canvas.Brush.Color := clWhite;
  Canvas.Ellipse(BallPos, 2, BallPos + BallSize, 2 + BallSize);

  // Desenha o Caption à direita
  if FCaption <> '' then
  begin
    TextRect.Left := SwitchWidth + 5;
    TextRect.Top := 0;
    TextRect.Right := Width;
    TextRect.Bottom := Height;
    Canvas.Brush.Style := bsClear;
    Canvas.TextRect(TextRect, TextRect.Left, (Height - Canvas.TextHeight(FCaption)) div 2, FCaption);
  end;
end;

end.
